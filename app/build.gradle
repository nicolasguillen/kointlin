apply plugin: 'com.android.application'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.triplet.play'

android {

    compileSdkVersion buildConfig.compileSdk

    playAccountConfigs {
        production {
            serviceAccountEmail = propOrEmpty('KOINTLIN_UPLOAD_PLAY_EMAIL')
            pk12File = file('upload.p12')
        }
    }

    defaultConfig {
        applicationId "com.nicolasguillen.kointlin"

        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk

        versionCode buildConfig.version.code
        versionName buildConfig.version.name

        vectorDrawables.useSupportLibrary = true

        playAccountConfig = playAccountConfigs.production
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'kointlin'
            keyAlias 'kointlin'
            keyPassword 'kointlin'
        }
        if (file('kointlin.keystore').exists()) {
            upload {
                storeFile file('kointlin.keystore')
                storePassword propOrEmpty('KOINTLIN_UPLOAD_STORE_PASSWORD')
                keyAlias 'kointlin'
                keyPassword propOrEmpty('KOINTLIN_UPLOAD_KEY_PASSWORD')
            }
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            if (file('kointlin.keystore').exists()) {
                signingConfig signingConfigs.upload
            } else {
                signingConfig signingConfigs.debug
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    kapt {
        generateStubs = true
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

dependencies {
    implementation deps.kotlin.stdlib.jdk

    implementation deps.android.support.appCompat
    implementation deps.android.support.design
    implementation deps.android.support.recyclerView
    implementation deps.android.support.cardView
    implementation deps.android.support.annotations
    implementation deps.android.support.constraintLayout

    implementation deps.android.x.coreKtx

    implementation deps.rx.java
    implementation deps.rx.android
    implementation deps.rx.kotlin
    implementation deps.rx.binding

    implementation deps.retrofit.client
    implementation deps.retrofit.adapter
    implementation deps.retrofit.converterGson

    implementation deps.okhttp.client
    implementation deps.okhttp.logging

    implementation deps.picasso

    implementation deps.dagger.runtime
    annotationProcessor deps.dagger.compiler
    kapt deps.dagger.compiler

    implementation deps.android.arch.roomRuntime
    implementation deps.android.arch.roomRx
    kapt deps.android.arch.compiler

    testImplementation deps.junit
    testImplementation deps.mockito
    testImplementation deps.android.test.runner

}

play {
    track = 'beta'
    uploadImages = false
}
